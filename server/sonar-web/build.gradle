sonarqube {
  properties {
    property "sonar.projectName", "${projectTitle} :: Web"
    property "sonar.sources", "src/main/js"
    property "sonar.tests", "src/main/js"
    property "sonar.test.inclusions", "src/main/js/**/__tests__/**"
    property "sonar.exclusions", "src/main/js/libs/third-party/**/*,src/main/js/libs/require.js,src/main/js/**/__tests__/**"
  }
}

apply plugin: 'com.moowork.node'
apply plugin: 'war'

configurations {
  branding
}
dependencies {
  branding 'com.sonarsource:sonarsource-branding:1.3.0.307@war'
}
def brandingDir = "${buildDir}/branding"

task unzipBranding(type: Copy) {
  from { zipTree(configurations.branding.singleFile) }
  into brandingDir
}
task copyBranding1(type: Copy, dependsOn: unzipBranding) {
  into projectDir
  from file(brandingDir)
  include '**/*.js'
  include '**/*.svg'
  includeEmptyDirs = false
}
task copyBranding2(type: Copy, dependsOn: unzipBranding) {
  into "${projectDir}/src/main/webapp"
  from file(brandingDir)
  include '**/branding'
}
task copyBranding() {
  dependsOn copyBranding1
  dependsOn copyBranding2
}

node {
  version = '8.5.0'
  yarnVersion = '1.1.0'
  download = true
}

yarn_run {
  if (release) { dependsOn copyBranding }
  inputs.dir('scripts')
  inputs.dir('src/main/js')
  inputs.file('build.gradle')
  outputs.dir('src/main/webapp')

  args = ['build']
}

war.dependsOn yarn_run
